import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

buildscript {
    ext {
        springCloudAzureVersion = "5.19.0"
        // set gradle properties regarding nexus
        nexus_url = project.findProperty('nexus_url') ?: System.getenv('NEXUS_HOST')
        nexus_user = project.findProperty('nexus_user') ?: System.getenv('NEXUS_USERNAME')
        nexus_pw = project.findProperty('nexus_pw') ?: System.getenv('NEXUS_PASSWORD')
        no_nexus = project.findProperty('no_nexus') ?: System.getenv('NO_NEXUS') ?: false
        if (!no_nexus && (nexus_url == null || nexus_user == null || nexus_pw == null)) {
            throw new GradleException("property 'no_nexus' is set to false, but neither " +
                "'nexus_url', 'nexus_user' nor 'nexus_pw' is configured. You can do so " +
                "by e.g. creating a gradle.properties file in your 'GRADLE_USER_HOME' " +
                "(by default '~/.gradle') folder and setting the nexus properties there.")
        }
        nexusFolderReleases = project.findProperty('nexus_folder_releases')
            ?: System.getenv('NEXUS_FOLDER_RELEASES') ?: "maven-releases"
        nexusFolderSnapshots = project.findProperty('nexus_folder_snapshots')
            ?: System.getenv('NEXUS_FOLDER_SNAPSHOTS') ?: "maven-snapshots"
    }
}

plugins {
    id 'jacoco'
    id 'maven-publish'
	id 'java'
	id 'org.springframework.boot' version '3.5.9'
	id 'io.spring.dependency-management' version '1.1.7'
    id "com.gorylenko.gradle-git-properties" version "2.4.1" // Adds git info on /actuator/info endpoint
    id "org.openapi.generator" version "7.10.0"
}

group = 'opendevstack'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
    if (!no_nexus) {
        def nexusMaven = { repoUrl ->
            maven {
                credentials {
                    username = "${nexus_user}"
                    password = "${nexus_pw}"
                }
                url repoUrl
            }
        }
        nexusMaven("${nexus_url}/repository/maven-public/")
        nexusMaven("${nexus_url}/repository/atlassian_public/")
    } else {
        mavenCentral()
    }
}

dependencyManagement {
    imports {
        mavenBom "com.azure.spring:spring-cloud-azure-dependencies:$springCloudAzureVersion"
    }
}

dependencies {
    // Release dependencies
	implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'com.azure.spring:spring-cloud-azure-starter-active-directory'
    implementation 'com.azure.spring:spring-cloud-azure-starter-actuator'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
    implementation "org.springframework:spring-aspects"
    implementation 'javax.cache:cache-api:1.1.1'
    implementation 'org.modelmapper:modelmapper:3.2.4'
    implementation 'org.ehcache:ehcache:3.10.8:jakarta'
    implementation 'com.networknt:json-schema-validator:1.5.7'
    implementation 'org.apache.tika:tika-core:3.2.2'
    implementation 'org.apache.commons:commons-text:1.11.0'
    implementation 'org.apache.commons:commons-collections4:4.5.0'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

    // Development dependencies
    implementation 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Testing dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'me.paulschwarz:spring-dotenv:3.0.0'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// TODO maybe rename this to 'testing'? to avoid confusion with the 'test' QA environment
tasks.named('test') {
	useJUnitPlatform()
    finalizedBy jacocoTestReport
}

// Git properties to show on /actuator/info endpoint
gitProperties {
    keys = [
            'git.branch',
            'git.closest.tag.name',
            'git.build.version',
            'git.commit.message.full',
            'git.commit.time',
            'git.commit.id.abbrev',
    ]
}

bootJar {
    archiveFileName = "app.jar"
    destinationDirectory = file("$buildDir/../docker")
    // TODO Verify if this is the right approach - Download dependencies for SonarQube analysis
    doLast {
        configurations.runtimeClasspath.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            copy {
                from artifact.file
                into 'build/dependencies'
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

openApiGenerate {
    generatorName = "spring"
    outputDir = "$projectDir/target/generated-sources/openapi-server-component_catalog"
    inputSpec = "$projectDir/openapi/openapi-component_catalog-v1.0.0.yaml"
    library = "spring-boot"

    globalProperties = [
            generateAliasAsModel: "true",
            apiDocs             : "true",
            modelDocs           : "true",
            apiTests            : "true",
            modelTests          : "true"
    ]

    additionalProperties = [
            useTags              : "true",
            interfaceOnly        : "true",
            groupId              : "component_catalog",
            artifactId           : "component_catalog-v0-server",
            artifactVersion      : "0.0.0-SNAPSHOT",
            apiPackage           : "org.opendevstack.component_catalog.server.api",
            modelPackage         : "org.opendevstack.component_catalog.server.model",
            configPackage        : "org.opendevstack.component_catalog.config",
            documentationProvider: "springdoc",
            generateBuilders     : "true",
            useSpringBoot3       : "true",
            useSpringController  : "true",
            useSwaggerUI         : "true",
            hideGenerationTimestamp: "true"
    ]
}

tasks.register("openApiGenerateBitbucketClient", GenerateTask) {
    generatorName = "java"
    outputDir = "$projectDir/target/generated-sources/openapi-client-bitbucket"
    inputSpec = "$projectDir/openapi/openapi-bitbucket-v89.yaml"
    library = "resttemplate"

    // Type and import mappings
    typeMappings.put("string+byte", "Resource")
    importMappings.put("Resource", "org.springframework.core.io.Resource")

    globalProperties = [
            generateAliasAsModel: "true",
            apiDocs             : "true",
            modelDocs           : "true",
            apiTests            : "false",
            modelTests          : "false"
    ]

    additionalProperties = [
            serializableModel               : "true",
            useJakartaEe                    : "true",
            java8                           : "true",
            groupId                         : "bitbucket",
            artifactId                      : "bitbucket-v89-client",
            artifactVersion                 : "0.0.0-SNAPSHOT",
            invokerPackage                  : "org.opendevstack.component_catalog.client.bitbucket.v89",
            apiPackage                      : "org.opendevstack.component_catalog.client.bitbucket.v89.api",
            modelPackage                    : "org.opendevstack.component_catalog.client.bitbucket.v89.model",
            additionalModelTypeAnnotations  : "@com.fasterxml.jackson.annotation.JsonInclude(com.fasterxml.jackson.annotation.JsonInclude.Include.NON_NULL)",
            generateBuilders                : "true",
            generateClientAsBean            : "true",
            useSingleRequestParameter       : "true",
            hideGenerationTimestamp         : "true"
    ]
}

tasks.register("openApiGenerateProjectInfoServicesClient", GenerateTask) {
    generatorName = "java"
    outputDir = "$projectDir/target/generated-sources/openapi-client-projects-info-service"
    inputSpec = "$projectDir/openapi/openapi-projects-info-service-v1.0.0.yaml"
    library = "resttemplate"

    // Type and import mappings
    typeMappings.put("string+byte", "Resource")
    importMappings.put("Resource", "org.springframework.core.io.Resource")

    globalProperties = [
            generateAliasAsModel: "true",
            apiDocs             : "true",
            modelDocs           : "true",
            apiTests            : "false",
            modelTests          : "false"
    ]

    additionalProperties = [
            serializableModel               : "true",
            useJakartaEe                    : "true",
            java8                           : "true",
            groupId                         : "bitbucket",
            artifactId                      : "bitbucket-v89-client",
            artifactVersion                 : "0.0.0-SNAPSHOT",
            invokerPackage                  : "org.opendevstack.component_catalog.client.projects_info_service.v1_0_0",
            apiPackage                      : "org.opendevstack.component_catalog.client.projects_info_service.v1_0_0.api",
            modelPackage                    : "org.opendevstack.component_catalog.client.projects_info_service.v1_0_0.model",
            additionalModelTypeAnnotations  : "@com.fasterxml.jackson.annotation.JsonInclude(com.fasterxml.jackson.annotation.JsonInclude.Include.NON_NULL)",
            generateBuilders                : "true",
            generateClientAsBean            : "true",
            useSingleRequestParameter       : "true",
            hideGenerationTimestamp         : "true"
    ]
}

// Ensure code generation runs before Java compilation
tasks.named("compileJava") {
    dependsOn(
            tasks.named("openApiGenerate"),
            tasks.named("openApiGenerateBitbucketClient"),
            tasks.named("openApiGenerateProjectInfoServicesClient")
    )
}

tasks.named("clean").configure {
    doLast {
        delete("$projectDir/target")
    }
}

sourceSets {
    main {
        java {
            srcDirs = [
                    "src/main/java",
                    "$projectDir/target/generated-sources/openapi-server-component_catalog/src/main/java",
                    "$projectDir/target/generated-sources/openapi-client-bitbucket/src/main/java",
                    "$projectDir/target/generated-sources/openapi-client-projects-info-service/src/main/java"
            ]
        }
        resources {
            srcDirs = ["src/main/resources"]
        }
    }
}

publishing {
    repositories {
        maven {
            url = "${nexus_url}/repository/" + (version.endsWith('SNAPSHOT') ? nexusFolderSnapshots : nexusFolderReleases)

            credentials {
                username = "${nexus_user}"
                password = "${nexus_pw}"
            }

        }
    }
    publications {
        maven(MavenPublication) {
            artifactId = rootProject.name
            groupId = group
            version = "${version}"
            from components.java
        }
    }
}

